# History file parameters (path to the history file, size of the history etc)
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000

# Bind keys to vi-mode instead of default emacs-mode
bindkey -v

function zle-line-init zle-keymap-select {
    RPS1="${${KEYMAP/vicmd/-- NORMAL --}/(main|viins)/-- INSERT --}"
    RPS2=$RPS1
    zle reset-prompt
    case $KEYMAP in
        vicmd) print -rn -- $terminfo[cvvis];; # block cursor
        viins) print -rn -- $terminfo[cnorm];; # less visible cursor
    esac
}
zle -N zle-line-init
zle -N zle-keymap-select

# Allow changing directory by its name, w/o cd
setopt auto_cd

# Allow piping to multiple outputs
setopt multios

# 
setopt extended_glob

# Path to my zsh configuration.
ZSH=$HOME/Config/dotfiles/zsh
# Path to additional config files
ZSH_CONFIG=$ZSH/config
# Path to zsh plugins
ZSH_PLUGINS=$ZSH/plugins


# Add /usr/local/bin to the path, so we can enable Homebrew & all its packages
export PATH=/usr/local/bin:$PATH

export GNUTERM='x11'

export EDITOR=vim
export SHELL=zsh

plugins=(ant brew bundler django gem git git-flow github heroku npm pip pow python rails3 rvm sublime thor vagrant vundle tmuxinator)

is_plugin() {
  local base_dir=$1
  local name=$2
  test -f $base_dir/plugins/$name/$name.plugin.zsh \
    || test -f $base_dir/plugins/$name/_$name
}

# Add all defined plugins to fpath. This must be done
# before running compinit.
for plugin ($plugins); do
  if is_plugin $ZSH_CUSTOM $plugin; then
    fpath=($ZSH_CUSTOM/plugins/$plugin $fpath)
  elif is_plugin $ZSH $plugin; then
    fpath=($ZSH/plugins/$plugin $fpath)
  fi
done

# Load all of the config files in $ZSH/lib that end in .zsh
# TIP: Add files you don't want in git to .gitignore
for config_file ($ZSH_CONFIG/*.zsh) source $config_file

# Load and run compinit for the tab completion
autoload -U compinit
compinit -i

# Load all of the plugins 
for plugin ($plugins); do
  if [ -f $ZSH/plugins/$plugin/$plugin.plugin.zsh ]; then
    source $ZSH/plugins/$plugin/$plugin.plugin.zsh
  fi
done

#COMPLETION_WAITING_DOTS="true"
# http://stackoverflow.com/a/844299
expand-or-complete-with-dots() {
  echo -n "\e[31m...\e[0m"
  zle expand-or-complete
  zle redisplay
}
zle -N expand-or-complete-with-dots
bindkey "^I" expand-or-complete-with-dots

ZSH_THEME="xenocid"

# Load the theme
source "$ZSH/themes/$ZSH_THEME.zsh-theme"

# use .localrc for settings specific to one system
[[ -f ~/.localrc ]] && . ~/.localrc

# Load the TMuxinator
[[ -s $HOME/.tmuxinator/scripts/tmuxinator ]] && source $HOME/.tmuxinator/scripts/tmuxinator
#
# # Load the RVM
[[ -s "$HOME/.rvm/scripts/rvm" ]] && source "$HOME/.rvm/scripts/rvm" # Load RVM into a shell session *as a function*

PATH=$PATH:$HOME/.rvm/bin:/usr/local/smlnj-110.75/bin # Add RVM to PATH for scripting
